Shader "Unlit/FadeIN"
{
	Properties

	{	
		[PerRendererData] _MainTex("Sprite Texture", 2D) = "white" {}	// テクスチャを指定
		_Color("Tint", Color) = (1,1,1,1)								// テクスチャに適用する色
		_Alpha ("Time", Range(0, 1)) = 0								// フェードを制御するための変数(0 : 透明, 1 : 不透明)
	}

	SubShader
	{
		Tags
		{
			"Queue" = "Transparent"
			"IgnoreProjector" = "True"
			"RenderType" = "Transparent"
			"PreviewType" = "Plane"
			"CanUseSpriteAtlas" = "True"
		}

		Cull Off
		Lighting Off
		ZWrite Off
		ZTest[unity_GUIZTestMode]
		Fog{ Mode Off }
		Blend SrcAlpha OneMinusSrcAlpha

		Pass
		{
			CGPROGRAM
#pragma vertex vert
#pragma fragment frag
#include "UnityCG.cginc"

			struct appdata_t
			{
				float4 vertex   : POSITION;
				float2 texcoord : TEXCOORD0;
			};

			struct v2f
			{
				float4 vertex   : SV_POSITION;
				half2 texcoord  : TEXCOORD0;
			};

			fixed4 _Color;
			uniform fixed _Alpha;
			sampler2D _MainTex;

			// 頂点シェーダー
			v2f vert(appdata_t IN)
			{
				v2f OUT;
				// オブジェクト空間の頂点位置をスクリーン空間に変換
				OUT.vertex = UnityObjectToClipPos(IN.vertex);
				
				// 各頂点に定められたUV設定
				OUT.texcoord = IN.texcoord;
#ifdef UNITY_HALF_TEXEL_OFFSET	// テクスチャのズレを補正するマクロ
				OUT.vertex.xy += (_ScreenParams.zw - 1.0) * float2(-1,1);
#endif
				return OUT;
			}

			// フラグメントシェーダー
			fixed4 frag(v2f IN) : SV_Target
			{
				// テクスチャのα値をalphaに格納する
				half alpha = tex2D(_MainTex, IN.texcoord).a;
				
				// saturateで計算結果を0, 1の範囲に制限する
				alpha = saturate(1 - alpha - (_Alpha * 2 - 1));		

				return fixed4(_Color.r, _Color.g, _Color.b, alpha);
			}
			ENDCG
		}
	}

	FallBack "UI/Default"
}